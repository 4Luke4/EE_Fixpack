DEFINE_ACTION_FUNCTION "HOLD_CREATURE"
BEGIN
	/*
	****************************************************************************************
	***************************************** ITM files ************************************
	****************************************************************************************
	*/

	WITH_SCOPE BEGIN
		ACTION_MATCH 1 WITH
			(GAME_IS ~bgee~) BEGIN
				ACTION_DEFINE_ASSOCIATIVE_ARRAY "patch_data" BEGIN
					// parent resref => child resref
					//"shld07p.itm" "override"
					"sw1p01" => "sw1p01b" // Viper's Edge +2
				END
			END
			(GAME_IS ~bg2ee eet~) BEGIN
				ACTION_DEFINE_ASSOCIATIVE_ARRAY "patch_data" BEGIN
					// parent resref => child resref
				END
			END
			(GAME_IS ~iwdee~) BEGIN
				ACTION_DEFINE_ASSOCIATIVE_ARRAY "patch_data" BEGIN
					// parent resref => child resref
					"ulswd4a" => "ulswd4a" // Hold Fast +3
				END
			END
			DEFAULT
				FAIL "Game not supported"
		END
		/* Main */
		ACTION_PHP_EACH "patch_data" AS "parent_resref" => "child_resref" BEGIN
			COPY_EXISTING "%parent_resref%.itm" "override"
				// Transfer effects onto a new SPL file (if needed)
				PATCH_IF ("%child_resref%" STRING_COMPARE_CASE "") BEGIN
					LAUNCH_PATCH_FUNCTION "gt_extract_effects_as_subspell"
					INT_VAR
						"effectID" = 175
					STR_VAR
						"subspell_resref" = "%child_resref%"
						"string" = "^Held$"
						"icon" = "^13$"
					END
				END ELSE BEGIN
					LAUNCH_PATCH_FUNCTION "gt_reorder_effect_stack" INT_VAR "effectID" = 175 END
				END
			BUT_ONLY_IF_IT_CHANGES IF_EXISTS
		END
	END

	/*
	****************************************************************************************
	***************************************** SPL files ************************************
	****************************************************************************************
	*/

	WITH_SCOPE BEGIN
		ACTION_MATCH 1 WITH
			(GAME_IS ~bgee~) BEGIN
				ACTION_DEFINE_ASSOCIATIVE_ARRAY "patch_data" BEGIN
					// parent resref => child resref
					"spcl415" => "spcl415b" // Snare
					"%BEHOLDER_HOLD_PERSON%" => ""
					"%TANARI_DEATH_GAZE%" => "%TANARI_DEATH_GAZE%a"
					"%CLERIC_HOLD_PERSON%" => ""
					"%CLERIC_HOLD_ANIMAL%" => ""
					"%TRAP_HOLD_PERSON%" => ""
					"%WIZARD_HOLD_PERSON%" => ""
					"spwm122" => "" // Hold Person (wild surge)
				END
			END
			(GAME_IS ~bg2ee eet~) BEGIN
				ACTION_DEFINE_ASSOCIATIVE_ARRAY "patch_data" BEGIN
					// parent resref => child resref
					"ohnwand1" => "ohnwand2"
					"spcl415" => "spcl415b" // Snare
					"%HOLD_PERSON_NO_SAVE%" => ""
					"%BEHOLDER_HOLD_PERSON%" => ""
					"%TANARI_DEATH_GAZE%" => "%TANARI_DEATH_GAZE%a"
					"%CLERIC_HOLD_PERSON%" => ""
					"%CLERIC_HOLD_ANIMAL%" => ""
					"%CLERIC_IMPLOSION%" => "%CLERIC_IMPLOSION%a"
					"%TRAP_HOLD_PERSON%" => ""
					"%WIZARD_HOLD_PERSON%" => ""
					"%WIZARD_HOLD_MONSTER%" => ""
					"spwm114" => "" // Polymorph Other (wild surge)
					"spwm122" => "" // Hold Person (wild surge)
				END
			END
			(GAME_IS ~iwdee~) BEGIN
				ACTION_DEFINE_ASSOCIATIVE_ARRAY "patch_data" BEGIN
					// parent resref => child resref
					"spcl415" => "spcl415b"
					"%TRAP_POISON_PARALYZATION_DARTS%" => "%TRAP_POISON_PARALYZATION_DARTS%b"
				END
			END
			DEFAULT
				FAIL "Game not supported"
		END
		/* Main */
		ACTION_PHP_EACH "patch_data" AS "parent_resref" => "child_resref" BEGIN
			COPY_EXISTING "%parent_resref%.spl" "override"
				// Transfer effects onto a new SPL file (if needed)
				PATCH_IF ("%child_resref%" STRING_COMPARE_CASE "") BEGIN
					LAUNCH_PATCH_FUNCTION "gt_extract_effects_as_subspell"
					INT_VAR
						"effectID" = 175
					STR_VAR
						"subspell_resref" = "%child_resref%"
						"string" = "^Held$"
						"icon" = "^13$"
					END
				END ELSE BEGIN
					LAUNCH_PATCH_FUNCTION "gt_reorder_effect_stack" INT_VAR "effectID" = 175 END
				END
			BUT_ONLY_IF_IT_CHANGES IF_EXISTS
		END
	END
END

/////////////////////////////////////////////////////////////////
/*

Collect SPL/ITM files to check

*/
//////////////////////////////////////////////////////////////////

/*
WITH_SCOPE BEGIN
	OUTER_SET "count_spl" = 0
	OUTER_SET "count_itm" = 0
	COPY_EXISTING_REGEXP - "^.+\.\(spl\|itm\)$" "override"
		PATCH_MATCH "%DEST_EXT%" WITH
			"itm" BEGIN
				GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
				TEXT_SPRINT "filename" "rearrange_itm"
			END
			"spl" BEGIN
				GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
				TEXT_SPRINT "filename" "rearrange_spl"
			END
			DEFAULT
				PATCH_FAIL "~%DEST_FILE%~"
		END
		PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
			PATCH_IF SHORT_AT ("%ab_off%" + 0x1E) > 1 BEGIN
				GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
				PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
					PATCH_MATCH SHORT_AT "%fx_off%" WITH
						175 BEGIN
							INNER_ACTION BEGIN
								ACTION_IF ("%DEST_EXT%" STRING_EQUAL_CASE "itm") BEGIN
									OUTER_SET "count_itm" += 1
									APPEND_OUTER "%MOD_FOLDER%/%filename%.txt" "%count_itm%) ~%DEST_FILE%~ => ability #%ab_ind%, position #%fx_ind%" UNLESS "%count_itm%) ~%DEST_FILE%~ => ability #%ab_ind%, position #%fx_ind%"
								END ELSE BEGIN
									OUTER_SET "count_spl" += 1
									APPEND_OUTER "%MOD_FOLDER%/%filename%.txt" "%count_spl%) ~%DEST_FILE%~ => ability #%ab_ind%, position #%fx_ind%" UNLESS "%count_spl%) ~%DEST_FILE%~ => ability #%ab_ind%, position #%fx_ind%"
								END
							END
						END
						DEFAULT
					END
				END
			END
		END
	BUT_ONLY_IF_IT_CHANGES
END
*/