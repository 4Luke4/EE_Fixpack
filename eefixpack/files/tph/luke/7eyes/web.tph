DEFINE_ACTION_FUNCTION "WEB"
BEGIN
	WITH_SCOPE BEGIN
		LAF "ADD_IDS_ENTRY" STR_VAR "idsFile" = "splstate" "identifier" = "WEB_IMMUNITY" END
		//
		ACTION_MATCH 1 WITH
			(GAME_IS ~bgee~) BEGIN
				ACTION_DEFINE_ASSOCIATIVE_ARRAY "patch_data" BEGIN
					// parent file => child resref
					"bdspidga.spl" => "" // Web Tangle (Gargantuan Spider)
					"bdwyrmt.spl" => "bdwyrmtw" // Tail Constriction
					"magiweb.itm" => "magiweb" // unused...?
					"spdr201.spl" => "" // Web (Avenger)
					"%FIGURINE_SPIDER_WEB%.spl" => ""
					"%WIZARD_WEB%.spl" => ""
				END
			END
			(GAME_IS ~bg2ee eet~) BEGIN
				ACTION_DEFINE_ASSOCIATIVE_ARRAY "patch_data" BEGIN
					// parent resref => child resref
					"kuobolt3.itm" => "kuobolt3" // Kuo-toan Bolt
					"magiweb.itm" => "magiweb" // unused...?
					"spdr201.spl" => "" // Web (Avenger)
					"%MIMIC_WEB%.spl" => ""
					"%FIGURINE_SPIDER_WEB%.spl" => ""
					"%WIZARD_WEB%.spl" => ""
					"wand14.itm" => "wand14" // Web sack
				END
			END
			(GAME_IS ~iwdee~) BEGIN
				ACTION_DEFINE_ASSOCIATIVE_ARRAY "patch_data" BEGIN
					// parent file => child resref
					"spdr201" => ""
					"%WIZARD_WEB%" => ""
				END
			END
			DEFAULT
				FAIL "Game not supported"
		END
		/* Main */
		ACTION_PHP_EACH "patch_data" AS "parent_file" => "child_resref" BEGIN
			COPY_EXISTING "%parent_file%" "override"
				// Transfer effects onto a new SPL file (if needed)
				PATCH_IF ("%child_resref%" STRING_COMPARE_CASE "") BEGIN
					LAUNCH_PATCH_FUNCTION "gt_extract_effects_as_subspell"
					INT_VAR
						"effectID" = 157
					STR_VAR
						"subspell_resref" = "%child_resref%"
						"string" = "Webbed, Held, Constricted"
						"icon" = "129"
						"splstate" = "WEB_IMMUNITY"
					END
				END ELSE BEGIN
					LAUNCH_PATCH_FUNCTION "gt_reorder_effect_stack" INT_VAR "effectID" = 157 END
				END
			BUT_ONLY_IF_IT_CHANGES IF_EXISTS
		END
	END
END

/////////////////////////////////////////////////////////////////
/*

Collect SPL/ITM files to check

*/
//////////////////////////////////////////////////////////////////

/*
WITH_SCOPE BEGIN
	OUTER_SET "count_spl" = 0
	OUTER_SET "count_itm" = 0
	COPY_EXISTING_REGEXP - "^.+\.\(spl\|itm\)$" "override"
		PATCH_MATCH "%DEST_EXT%" WITH
			"itm" BEGIN
				GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
				TEXT_SPRINT "filename" "rearrange_itm"
			END
			"spl" BEGIN
				GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
				TEXT_SPRINT "filename" "rearrange_spl"
			END
			DEFAULT
				PATCH_FAIL "~%DEST_FILE%~"
		END
		PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
			PATCH_IF SHORT_AT ("%ab_off%" + 0x1E) > 1 BEGIN
				GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
				PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
					PATCH_MATCH SHORT_AT "%fx_off%" WITH
						157 BEGIN
							INNER_ACTION BEGIN
								ACTION_IF ("%DEST_EXT%" STRING_EQUAL_CASE "itm") BEGIN
									OUTER_SET "count_itm" += 1
									APPEND_OUTER "%MOD_FOLDER%/%filename%.txt" "%count_itm%) ~%DEST_FILE%~ => ability #%ab_ind%, position #%fx_ind%" UNLESS "%DEST_FILE%"
								END ELSE BEGIN
									OUTER_SET "count_spl" += 1
									APPEND_OUTER "%MOD_FOLDER%/%filename%.txt" "%count_spl%) ~%DEST_FILE%~ => ability #%ab_ind%, position #%fx_ind%" UNLESS "%DEST_FILE%"
								END
							END
						END
						DEFAULT
					END
				END
			END
		END
	BUT_ONLY_IF_IT_CHANGES
END
*/