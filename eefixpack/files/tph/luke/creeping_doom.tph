DEFINE_ACTION_FUNCTION "CLERIC_CREEPING_DOOM"
BEGIN
	WITH_SCOPE BEGIN
		COPY_EXISTING "%CLERIC_CREEPING_DOOM%.spl" "override"
			LPF "ALTER_SPELL_HEADER"
			INT_VAR
				"projectile" = IDS_OF_SYMBOL ("MISSILE" "Chain_Insect")
			END
			PATCH_WITH_SCOPE BEGIN
				GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
				PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
					SET "found" = 0
					GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
					PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
						PATCH_MATCH SHORT_AT "%fx_off%" WITH
							233 WHEN (LONG_AT ("%fx_off%" + 0x8) == IDS_OF_SYMBOL ("STATS" "CLERIC_INSECT_PLAGUE")) BEGIN
								PATCH_IF !("%found%") BEGIN
									SET "found" = 1
								END ELSE BEGIN
									WRITE_SHORT "%fx_off%" 999 // mark it for later deletion
								END
							END
							DEFAULT
						END
					END
					LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 999 ~check_globals~ = 0 ~header~ = ~%ab_ind%~ END
				END
			END
			/* "For each round the victim remains inside the cloud, <PRO_HESHE> must make a Save vs. Spell at -2 or run away in fear for one round."
			** Since the spell's projectile is coded as `# repetitions = 1`, the savingthrow check must be put in the subspell!
			** In so doing, you'll correctly get one savingthrow check per round instead of just one savingthrow check!
			*/
			LPF "ALTER_EFFECT"
			INT_VAR
				"match_opcode" = 272 // Use EFF file on condition
				"opcode" = 146 // Cast spell
				"parameter1" = 0
				"parameter2" = 1 // Cast instantly (ignore level)
				"timing" = 4
				"duration" = 0
			STR_VAR
				"match_resource" = "panic"
				"resource" = "%CLERIC_CREEPING_DOOM%a"
			END
			LPF "CLONE_EFFECT"
			INT_VAR
				"match_opcode" = 146 // Cast spell
				"duration" = 6
			STR_VAR
				"insert" = "below"
			END
			LPF "CLONE_EFFECT"
			INT_VAR
				"match_opcode" = 146 // Cast spell
				"match_duration" = 6
				"duration" = 12
			STR_VAR
				"insert" = "below"
			END
		BUT_ONLY_IF_IT_CHANGES
	END
	// Subspell applying the Panic effect(s)
	WITH_SCOPE BEGIN
		COPY_EXISTING "%CLERIC_CREEPING_DOOM%.spl" "override/%CLERIC_CREEPING_DOOM%a.spl"
			/* Header */
			WRITE_LONG NAME1 "-1"
			WRITE_ASCII 0x10 "" #8 // casting sound
			WRITE_LONG 0x18 0 // flags
			WRITE_LONG 0x1E 0 // exclusion flags
			WRITE_SHORT 0x22 0 // casting animation
			/* Extended header(s) */
			LPF "ALTER_SPELL_HEADER"
			INT_VAR
				"projectile" = IDS_OF_SYMBOL ("MISSILE" "None")
				"speed" = 0
			END
			/* Feature block(s) */
			LPF "DELETE_EFFECT" END // delete current content
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 24 // Panic
				"target" = 2 // Preset target
				"duration" = 6
				"resist_dispel" = BIT0 + BIT1
				"savingthrow" = BIT0 // Save vs. Spell
				"savebonus" = "-2"
			END
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 142 // Display portrait icon
				"target" = 2 // Preset target
				"parameter2" = 36 // Panic
				"duration" = 6
				"resist_dispel" = BIT0 + BIT1
				"savingthrow" = BIT0 // Save vs. Spell
				"savebonus" = "-2"
			END
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 139 // Display string
				"target" = 2 // Preset target
				"parameter1" = 14007 // ~Panic~
				"timing" = 1
				"resist_dispel" = BIT0 + BIT1
				"savingthrow" = BIT0 // Save vs. Spell
				"savebonus" = "-2"
			END
		BUT_ONLY_IF_IT_CHANGES
	END
END