// This function moves Holy / Unholy Word so as to use 324 magic rather than those messy 177s.
// The new implementation takes into account the "7eyes.2da" thing... More on that on a future pull request...

DEFINE_ACTION_FUNCTION "CLERIC_HOLY_UNHOLY_WORD"
BEGIN
	WITH_SCOPE BEGIN
		COPY_EXISTING "%CLERIC_HOLY_WORD%.spl" "override" "%CLERIC_UNHOLY_WORD%.spl" "override"
			LPF "DELETE_EFFECT"
			INT_VAR
				"check_globals" = 0
				"match_opcode" = 177 // Use EFF file
			END
			PATCH_WITH_SCOPE BEGIN
				PATCH_MATCH "%DEST_RES%" WITH
					"%CLERIC_HOLY_WORD%" BEGIN
						LPF "ADD_SPLPROT_ENTRY"
						INT_VAR
							"stat" = 0x104 // negate 0x103 (use two rows of "splprot.2da")
						STR_VAR
							"value" = "37" // ALIGNMENTBITS >= MASK_EVIL
							"relation" = "1" // GENERAL = UNDEAD
							"label" = "ENTRIES != (37 || 1)" // !(ALIGNMENTBITS>=MASK_EVIL || GENERAL=UNDEAD)
						RET
							"index"
						END
					END
					"%CLERIC_UNHOLY_WORD%" BEGIN
						SET "index" = 36 // ALIGNMENTBITS!=MASK_GOOD
					END
					DEFAULT
				END
				LPF "ADD_SPELL_EFFECT"
				INT_VAR
					"opcode" = 324 // Immunity to resource and message
					"target" = 2 // Preset target
					"power" = 7
					"parameter2" = "%index%"
					"resist_dispel" = BIT1
				STR_VAR
					"resource" = "%DEST_RES%"
				END
			END
			// Less than 4 Hit Dice / Levels => Death
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 13 // Kill target
				"target" = 2 // Preset target
				"power" = 7
				"timing" = 1
				"parameter2" = BIT2 // Normal death
				"resist_dispel" = BIT0
				"dicenumber" = 3
			END
			// 4 to 7 Hit Dice / Levels => Stunned for 1 turn
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 45 // Stun
				"target" = 2 // Preset target
				"power" = 7
				"duration" = 60 // 1 turn
				"resist_dispel" = BIT0
				"dicenumber" = 7
				"dicesize" = 4
			END
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 139 // Display string
				"target" = 2 // Preset target
				"power" = 7
				"parameter1" = "%game_is_bgee%" ? 26050 : 1280
				"timing" = 1
				"resist_dispel" = BIT0
				"dicenumber" = 7
				"dicesize" = 4
			END
			// 8 to 11 Hit Dice / Levels => Slowed for 1 turn with a 75% chance of spell failure
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 40 // Slow
				"target" = 2 // Preset target
				"power" = 7
				"duration" = 60 // 1 turn
				"resist_dispel" = BIT0
				"dicenumber" = 11
				"dicesize" = 8
			END
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 139 // Display string
				"target" = 2 // Preset target
				"power" = 7
				"parameter1" = 14668
				"timing" = 1
				"resist_dispel" = BIT0
				"dicenumber" = 11
				"dicesize" = 8
			END
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 60 // Casting failure
				"target" = 2 // Preset target
				"power" = 7
				"parameter1" = 75
				"parameter2" = 0 // Arcane spells
				"duration" = 60 // 1 turn
				"resist_dispel" = BIT0
				"dicenumber" = 11
				"dicesize" = 8
			END
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 60 // Casting failure
				"target" = 2 // Preset target
				"power" = 7
				"parameter1" = 75
				"parameter2" = 1 // Divine spells
				"duration" = 60 // 1 turn
				"resist_dispel" = BIT0
				"dicenumber" = 11
				"dicesize" = 8
			END
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 142 // Display portrait icon
				"target" = 2 // Preset target
				"power" = 7
				"parameter2" = 105 // Miscast Magic
				"duration" = 60 // 1 turn
				"resist_dispel" = BIT0
				"dicenumber" = 11
				"dicesize" = 8
			END
			// 12+ Hit Dice / Levels => Deafened for 1 turn with a 50% chance of spell failure
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 80 // Deafness
				"target" = 2 // Preset target
				"power" = 7
				"duration" = 60 // 1 turn
				"resist_dispel" = BIT0
				"dicesize" = 12
			END
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 142 // Display portrait icon
				"target" = 2 // Preset target
				"power" = 7
				"parameter2" = 112 // Deaf
				"duration" = 60 // 1 turn
				"resist_dispel" = BIT0
				"dicesize" = 12
			END
			// Visual / Cosmetic effects
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 61 // Creature RGB color fade
				"target" = 2 // Preset target
				"power" = 7
				"parameter1" = ("%DEST_RES%" STR_EQ "%CLERIC_HOLY_WORD%") ? (128 << 8) + (128 << 16) + (128 << 24) : (160 << 8) + (32 << 16) + (0 << 24)
				"parameter2" = 0 + (25 << 16)
				"timing" = 1
				"resist_dispel" = BIT0
			END
			LPF "ADD_SPELL_EFFECT"
			INT_VAR
				"opcode" = 141 // Lighting effects
				"target" = 2 // Preset target
				"power" = 7
				"parameter2" = 22 // Conjure water
				"timing" = 1
				"resist_dispel" = BIT0
			END
		BUT_ONLY_IF_IT_CHANGES
	END
END