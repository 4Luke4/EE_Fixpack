/////                                                  \\\\\
///// string fixes                                     \\\\\
/////                                                  \\\\\

// string fixes, processed automatically if tra file exists
LAUNCH_ACTION_FUNCTION cd_string_set_from_tra
  STR_VAR input_tra  = EVAL ~eefixpack/languages/%LANGUAGE%/fixes_%game%.tra~ // tra file to process
          output_tph = EVAL ~weidu_external/eefixpack/fixes_%game%.tph~ // tph being built; use weidu_external for this
END
ACTION_IF game_includes_sod BEGIN
  LAUNCH_ACTION_FUNCTION cd_string_set_from_tra
    STR_VAR input_tra  = EVAL ~eefixpack/languages/%LANGUAGE%/fixes_%game%_sod.tra~ // tra file to process
            output_tph = EVAL ~weidu_external/eefixpack/fixes_%game%_sod.tph~ // tph being built; use weidu_external for this
  END
END

/////                                                  \\\\\
///// lua fixes                                        \\\\\
/////                                                  \\\\\

COPY_EXISTING ~bgee.lua~ ~override~
  PATCH_IF game_includes_sod BEGIN
    REPLACE_TEXTUALLY ~\({'BDORCM1L',[ %TAB%]*1},\)~ ~\1%WNL%%TAB%{'HELM',%TAB%1},%WNL%%TAB%{'HVLN',%TAB%1},~
    REPLACE_TEXTUALLY ~\({'BDORCF1L',[ %TAB%]*2},\)~ ~\1%WNL%%TAB%{'SKAN',%TAB%2},~
/* comment this section, pending verification for mobile versions   
    REPLACE_TEXTUALLY ~\({'BAELOTH',[ %TAB%]*1},\)~ ~\1%WNL%%TAB%{'NCERND',%TAB%1},%WNL%%TAB%{'NKELDOR',%TAB%1},%WNL%%TAB%{'NANOMEN',%TAB%1},%WNL%%TAB%{'NEDWIN',%TAB%1},%WNL%%TAB%{'NHAER',%TAB%1},%WNL%%TAB%{'NJAN',%TAB%1},%WNL%%TAB%{'NMINSC',%TAB%1},%WNL%%TAB%{'NVALYGA',%TAB%1},%WNL%%TAB%{'NYOSHIM',%TAB%1},%WNL%%TAB%{'NKORGAN',%TAB%1},~
    REPLACE_TEXTUALLY ~\({'NEERA',[ %TAB%]*2},\)~ ~\1%WNL%%TAB%{'NNALIA', 2},%WNL%%TAB%{'NMAZZY', 2},%WNL%%TAB%{'NAERIE', 2},~
  END ELSE BEGIN
    PATCH_FOR_EACH portrait IN BAELOTH NCERND NKELDOR NANOMEN NEDWIN NHAER NJAN NMINSC NVALYGA NYOSHIM NKORGAN NNALIA NMAZZY NAERIE NJAHEIR NVICON BEGIN
      REPLACE_TEXTUALLY ~--\({'%portrait%',[ %TAB%]*[12]},\)~ ~\1~
    END
*/
  END
  BUT_ONLY 
  
/////                                                  \\\\\
///// ids/2da fixes                                    \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/a7/anim_3001.tph~ // fix missing creature animation definitions for Neothelid

// tbd, cam (from jmerry)
// clck31 available in bp, but missing in armor exclusionlist (fixed in sod)
APPEND ~itemexcl.2da~ ~clck31 1~ UNLESS ~^clck31[ %TAB%]~ // clck31 available via black pits

/*
luke
**splprot.2da**
- the newly added (v2.6) header descriptions are incorrect for the alignment rows
*/
WITH_SCOPE BEGIN
  COPY_EXISTING "splprot.2da" "override"
    COUNT_2DA_COLS "cols"
    SET_2DA_ENTRY 33 0 "%cols%" "33_ALIGNMENTBITS=GOOD" // NOT BIT1 = Not NEUTRAL or EVIL
    SET_2DA_ENTRY 34 0 "%cols%" "34_ALIGNMENTBITS!=GOOD" // HAS BIT1 = Is NEUTRAL or EVIL
    SET_2DA_ENTRY 35 0 "%cols%" "35_ALIGNMENTBITS=NEUTRAL" // NOT BIT0 = Not GOOD or EVIL
    SET_2DA_ENTRY 36 0 "%cols%" "36_ALIGNMENTBITS!=NEUTRAL" // HAS BIT0 = Is GOOD or EVIL
    SET_2DA_ENTRY 37 0 "%cols%" "37_ALIGNMENTBITS=EVIL"
    SET_2DA_ENTRY 38 0 "%cols%" "38_ALIGNMENTBITS!=EVIL"
    SET_2DA_ENTRY 59 0 "%cols%" "59_ALIGNMENTBITS=LAWFUL" // NOT BIT5 = Not CHAOTIC or NEUTRAL
    SET_2DA_ENTRY 60 0 "%cols%" "60_ALIGNMENTBITS!=LAWFUL" // HAS BIT5 = Is CHAOTIC or NEUTRAL
    SET_2DA_ENTRY 61 0 "%cols%" "61_ALIGNMENTBITS=CHAOTIC"
    SET_2DA_ENTRY 62 0 "%cols%" "62_ALIGNMENTBITS!=CHAOTIC"
    SET_2DA_ENTRY 118 0 "%cols%" "118_ALIGNMENTBITS!=n" // Clearly state it is a bitwise check
    // Formatting
    PRETTY_PRINT_2DA
  BUT_ONLY_IF_IT_CHANGES
END

INCLUDE ~eefixpack/files/tph/clswpbon.tpa~ // tbd, cam: fix non-prof penalties for shadowdancers, mage-thieves

// Add missing "SPELL.IDS" entries (borrowed from SCS)
WITH_SCOPE BEGIN
  ACTION_CLEAR_ARRAY "spell_ids_missing"
  ACTION_DEFINE_ASSOCIATIVE_ARRAY "spell_ids_missing" BEGIN
    // Divine
    "1718" => "CLERIC_SYMBOL_STUN"
    "1719" => "CLERIC_SYMBOL_DEATH"
    // Arcane
    "2222" => "WIZARD_CHAOS_SHIELD"
    "2723" => "WIZARD_IMPROVED_CHAOS_SHIELD"
    "2490" => "WIZARD_POLYMORPH_NATURAL_FORM"
    "2493" => "WIZARD_POLYMORPH_FLIND"
    "2494" => "WIZARD_POLYMORPH_OGRE"
    "2495" => "WIZARD_POLYMORPH_SPIDER"
    "2496" => "WIZARD_POLYMORPH_MUSTARD_JELLY"
    "2497" => "WIZARD_POLYMORPH_BROWN_BEAR"
    "2498" => "WIZARD_POLYMORPH_BLACK_BEAR"
    "2499" => "WIZARD_POLYMORPH_WOLF"
    "2931" => "SUCCUBUS_TELEPORT"
    "2985" => "RED_HOLY_MIGHT"
    // Innate
    "3101" => "INNATE_CURE_LIGHT_WOUNDS"
    "3102" => "INNATE_SLOW_POISON"
    "3103" => "INNATE_DRAW_UPON_HOLY_MIGHT"
    "3104" => "INNATE_LARLOCHS_MINOR_DRAIN"
    "3105" => "INNATE_HORROR"
    "3106" => "INNATE_VAMPIRIC_TOUCH"
    "3122" => "AVENGER_SHAPESHIFT_NATURAL_FORM"
    "3123" => "DRUID_HUMAN_FORM"
    "3123" => "DRUID_SHAPESHIFT_FROM_BLACKBEAR"
    "3124" => "SHAPESHIFTER_SHAPESHIFT_NATURAL_FORM"
    "3150" => "SHAPESHIFT_NATURAL_FORM_1"
    "3151" => "SHAPESHIFT_NATURAL_FORM_2"
    "3152" => "SHAPECHANGE_MIND_FLAYER"
    "3153" => "SHAPECHANGE_IRON_GOLEM"
    "3154" => "SHAPECHANGE_GIANT_TROLL"
    "3155" => "SHAPECHANGE_GREATER_WOLFWERE"
    "3156" => "SHAPECHANGE_FIRE_ELEMENTAL"
    "3157" => "SHAPECHANGE_EARTH_ELEMENTAL"
    "3701" => "DEATHKNIGHT_FIREBALL"
    "3890" => "DEMON_FEAR"
    "3996" => "TANARI_DEATH_GAZE"
    "3997" => "TANARI_VAMPIRIC_TOUCH"
    "3998" => "TANARI_SILENCE"
    "3999" => "TANARI_PARALYZE"
    // Class
    "4212" => "PALADIN_DETECT_EVIL"
    "4213" => "PALADIN_PROTECTION_FROM_EVIL"
    "4611" => "DRUID_SHAPESHIFT_BROWNBEAR"
    "4612" => "DRUID_SHAPESHIFT_WOLF"
    "4613" => "DRUID_SHAPESHIFT_BLACKBEAR"
    "4632" => "AVENGER_SHAPESHIFT_SWORDSPIDER"
    "4633" => "AVENGER_SHAPESHIFT_BABYWYVERN"
    "4634" => "AVENGER_SHAPESHIFT_FIRESALAMANDER"
    "4721" => "TALOS_STORMSHIELD"
    "4722" => "TALOS_LIGHTNING_BOLT"
    "4732" => "HELM_TRUE_SIGHT"
  END
  ACTION_PHP_EACH "spell_ids_missing" AS "code" => "name" BEGIN
    APPEND "spell.ids" "%code% %name%" UNLESS "%code%[ %TAB%]+%name%"
  END
END

LAM "SPELL_IDS" // relaunch so as to take into account the BG2 entries

/////                                                  \\\\\
///// mass copy/includes actions                       \\\\\
/////                                                  \\\\\

COPY ~eefixpack/files/bam/ixbow04.bam~  ~override~ // tbd, cam: fix light crossbow BAM
     ~eefixpack/files/2da/clabmo02.2da~ ~override~ // tbd, cam: borked kit ability table, dark moon monk
     ~eefixpack/files/2da/clabmo03.2da~ ~override~ // tbd, cam: borked kit ability table, sun soul monk
     ~eefixpack/files/2da/clabrn02.2da~ ~override~ // tbd, cam: borked kit ability table, archer
     ~eefixpack/files/bam/mdem~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, demogorgon
     ~eefixpack/files/bam/mwyv~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, wyvern big
     ~eefixpack/files/bam/cmnk1inv.bam~ ~override~ // monk (tutorial, not pc) paperdoll
     ~eefixpack/files/bam/wphf2oin.bam~ ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wplf2inv.bam~ ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wplf2oin.bam~ ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2a1.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2a3.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2a5.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2a7.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2a8.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2a9.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2g1.bam~  ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2oa7.bam~ ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2oa8.bam~ ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2oa9.bam~ ~override~ // missing short flaming sword bams (f2)
     ~eefixpack/files/bam/wqnf2og1.bam~ ~override~ // missing short flaming sword bams (f2)

ACTION_IF game_includes_sod BEGIN

  COPY_EXISTING ~eefixpack/files/bam/mdr1~         ~override~ // tbd, sam.: merging four-frame animations to fix issues, dragon red
  
END

// tbd, sam.
// merging multi-part area animations to eliminate issues
ACTION_IF game_includes_sod BEGIN
  ACTION_FOR_EACH file IN bdfug01 bdfug02 bdfug03 bdfug04 BEGIN  
    COPY ~eefixpack/files/bam/area_anim/%file%.bam~ ~override~
  END  
END

// tbd, cam (from jmerry)
// curing poison should also cure insta-death from green slimes
INCLUDE ~eefixpack/files/tph/tbd_green_slime.tph~

// BGCS-3460, cam
// death tyrant animation fixes
INCLUDE ~eefixpack/files/tph/b3460_death_tyrant.tph~ // BGCS-3460, cam: death tyrant animation fixes

// bgcs-3397, cam
// revenant animation fixes
INCLUDE ~eefixpack/files/tph/b3397_revenant.tph~ // bgcs-3397, cam: revenant animation fixes

/////                                                  \\\\\
///// mechanics fixes / overhauls                      \\\\\
/////                                                  \\\\\

// kjeron
WITH_SCOPE BEGIN
  WITH_TRA "eefixpack/languages/en_us/luke/polymorph_overhaul.tra" "eefixpack/languages/%LANGUAGE%/luke/polymorph_overhaul.tra" BEGIN
    INCLUDE "eefixpack/files/tph/luke/polymorph_overhaul.tph"
    LAF "POLYMORPH_OVERHAUL" END
  END
END

/////                                                  \\\\\
///// dialogue fixes                                   \\\\\
/////                                                  \\\\\

COMPILE ~eefixpack/files/d/%game%_core_fixes.d~ // misc dialogue fixes

ACTION_IF game_includes_sod BEGIN
  COMPILE ~eefixpack/files/d/%game%_sod_core_fixes.d~ // misc dialogue fixes
END

/////                                                  \\\\\
///// script fixes                                     \\\\\
/////                                                  \\\\\

// tbd, cam
// xzar block loops the area script
COPY_EXISTING ~ar2700.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Exists("\(Xzar\|Imoen\|Montaron\)")~ ~InMyArea("\1")~
  END
  BUT_ONLY 
  
  

// tbd, cam
// can't reach the required deathcount for comments on lower difficulty since too many 'Followers' despawn
COPY_EXISTING ~bd0120.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~NumDeadGT("Followers",4)~ ~NumDeadGT("Followers",2)~
  END
  BUT_ONLY IF_EXISTS // sod
  
  
// tbd, cam
// syntax: an OR(4) only has two conditions
COPY_EXISTING ~bd1000.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR(4)~ ~OR(2)~ // luckily only one so easy replace
  END
  BUT_ONLY IF_EXISTS // sod asset



// tbd, cam
// neera checking the wrong state of the skull
COPY_EXISTING ~bd2000.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OpenState("Bhaal_Skull",TRUE)~ ~OpenState("Bhaal_Skull",FALSE)~ // wrong state
  END
  BUT_ONLY IF_EXISTS // sod



// tbd, cam
// trying to activate a region via Ambient activate
COPY_EXISTING ~bd2000.bcs~   ~override~
              ~bdbarghe.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~AmbientActivate("enlightenment"~ ~TriggerActivation("Enlightment"~ // yes, it's spelled that way
  END
  BUT_ONLY IF_EXISTS // sod



// tbd, cam
// referring to wrong DV for Halatathlaer
// wrong variable scope for timer
COPY_EXISTING ~bd5300.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~DisplayStringHead("bdhalat")~ ~DisplayStringHead("bdhalata")~ // script has a createcreature bdhalat, so need to extra bit of trigger to match
    REPLACE_TEXTUALLY ~"bd_sdd350b_ot_timer","bd2000"~ ~"bd_sdd350b_ot_timer","bd5300"~ // wrong variable scope
  END
  BUT_ONLY IF_EXISTS // sod




// tbd, cam
// referring to wrong DV for Imoen
COPY_EXISTING ~bd6200.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~"bdimoen"~ ~"imoen"~
  END
  BUT_ONLY IF_EXISTS // sod




// tbd, cam
// referring to wrong DV for Corwin
COPY_EXISTING ~bd7300.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~"bdcorwin"~ ~"corwin"~
  END
  BUT_ONLY IF_EXISTS // sod

// tbd, cam
// sod menhir quest fix: menhirs which spawn enemies on failure are checking/setting the wrong variable
ACTION_FOR_EACH script IN 1 4 BEGIN

  COPY_EXISTING ~bdmenhi%script%.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("activated","LOCALS"~ ~Global("BDMENHI%script%_Rune","GLOBAL"~
    END
    BUT_ONLY IF_EXISTS

END    

// tbd, cam
// possibly missing Minsc-rasaad banter due to typo
COPY_EXISTING ~bdminsc.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~"rasaad'"~ ~"rasaad"~
  END
  BUT_ONLY IF_EXISTS // sod




// tbd, cam
// saying 'go back to the crypt entrance' in korlasz crypt doesn't make NPCs do anything
COPY_EXISTING ~bdparty.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("dynaheir"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~ 
      ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[780.1575])~ // dynaheir save location
    REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("edwin"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~ 
      ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[780.1575])~ // edwin save location
    REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("baeloth"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~ 
      ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[840.1590])~ // baeloth save location
    REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("khalid"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~ 
      ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[840.1590])~ // khalid save location
    REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("safana"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~ 
      ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[870.1710])~ // safana save location
    REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("dorn"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~ 
      ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[920.1660])~ // dorn save location
    REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("minsc"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~ 
      ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[920.1660])~ // minsc save location
    REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("neera"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~ 
      ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[920.1660])~ // neera save location
    REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("jaheira"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~ 
      ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[935.1720])~ // jaheira save location
    REPLACE_TEXTUALLY ~\(TriggerOverride("ff_camp",IsOverMe("viconia"))[ %TAB%%LNL%%MNL%%WNL%]+Switch("bd_npc_camp_chapter","global")[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~ 
      ~\1 RESPONSE #0 SetGlobal("bd_npc_camp","locals",2) SaveLocation("LOCALS","bd_default_loc",[935.1720])~ // viconia save location
    REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("dynaheir"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~ ~\1 RESPONSE #0 EscapeAreaMove("bd0120",780,1575,NE)~ // move dynaheir
    REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("edwin"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~    ~\1 RESPONSE #0 EscapeAreaMove("bd0120",780,1575,NE)~ // move edwin
    REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("baeloth"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~  ~\1 RESPONSE #0 EscapeAreaMove("bd0120",840,1590,NE)~ // move baeloth
    REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("khalid"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~   ~\1 RESPONSE #0 EscapeAreaMove("bd0120",840,1590,NE)~ // move khalid
    REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("safana"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~   ~\1 RESPONSE #0 EscapeAreaMove("bd0120",870,1710,NE)~ // move safana
    REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("dorn"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~     ~\1 RESPONSE #0 EscapeAreaMove("bd0120",920,1660,NE)~ // move dorn
    REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("minsc"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~    ~\1 RESPONSE #0 EscapeAreaMove("bd0120",920,1660,NE)~ // move minsc
    REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("neera"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~    ~\1 RESPONSE #0 EscapeAreaMove("bd0120",920,1660,NE)~ // move neera
    REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("jaheira"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~  ~\1 RESPONSE #0 EscapeAreaMove("bd0120",935,1720,NE)~ // move jaheira
    REPLACE_TEXTUALLY ~\(!TriggerOverride("ff_camp",IsOverMe("viconia"))[ %TAB%%LNL%%MNL%%WNL%]+THEN\)~  ~\1 RESPONSE #0 EscapeAreaMove("bd0120",935,1720,NE)~ // move viconia
  END
  BUT_ONLY IF_EXISTS // sod
 

  

// tbd, cam
// spike trigger is a little too happy to murderize everyone, swap trigger
COPY_EXISTING ~bdlever2.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~WalkedToTrigger~ ~Clicked~
  END
  BUT_ONLY IF_EXISTS // sod

// tbd, cam (from jmerry)
// bp: hogarl not using the right potion when he's hurt
COPY_EXISTING ~bpgiafir.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(SetGlobalTimer("GIANT_POTION","LOCALS",ONE_ROUND)[%TAB% %LNL%%MNL%%WNL%]+\)UseItem("POTN27",NearestEnemyOf(Myself))~ ~\1 UseItem("POTN52",Myself)~
  END
  BUT_ONLY 

// tbd, cam (from jmerry)
// characters imported from bp retain their bp scripting
EXTEND_TOP ~bpplot.bcs~ ~eefixpack/files/baf/bpplotpatch.baf~ // BP script removes itself if not in BP area

// tbd, cam
// sod imoen missing xp-matching scripting, among other things
ACTION_IF game_includes_sod BEGIN
  EXTEND_BOTTOM ~imoen.bcs~ ~eefixpack/files/baf/sod_imoen_bottom.baf~
END



// tbd, cam
// NPCs should get their better starting equipment when doing the level-up thing; must follow the imoen.bcs script extension fix for sod games (currently immediately above)
COPY_EXISTING ~ajantis.bcs~ ~override~ // ajantis gets better armor
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,32000,SET)\)~
      ~\1 TransformItem("chan01","plat01")~ // chain > plate
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(16\|8\)000,SET)\)~
      ~\1 TransformItem("chan01","chan04")~ // chain > splint
  END
  BUT_ONLY 
  
COPY_EXISTING ~dorn.bcs~ ~override~ // dorn gets helmet at 9k, plate at 36k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,32000,SET)\)~
      ~\1 TransformItem("chan04","plat01")~ // splint > plate
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\)000,SET)\)~ 
      ~\1 CreateItem("helm01",0,0,0) XEquipItem("helm01",Myself,SLOT_HELMET,EQUIP)~ // add helmet
  END
  BUT_ONLY 
  
COPY_EXISTING ~edwin.bcs~ ~override~ // edwin gets throwing daggers at 41k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,32000,SET)\)~
      ~\1 CreateItem("dagg05",10,0,0) XEquipItem("dagg05",Myself,SLOT_WEAPON1,EQUIP)~ // add throwing daggers
  END
  BUT_ONLY 
  
COPY_EXISTING ~eldoth.bcs~ ~override~ // eldoth gets bow & arrows at 10k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\)000,SET)\)~ 
      ~\1 CreateItem("arow01",20,0,0) XEquipItem("arow01",Myself,SLOT_AMMO0,EQUIP) CreateItem("bow03",0,0,0) XEquipItem("bow03",Myself,SLOT_WEAPON1,EQUIP)~ // add bow, arrows
  END
  BUT_ONLY 
  
COPY_EXISTING ~imoen.bcs~ ~override~ // imoen short sword at 1300, studded leather at 20k; imoen can still use imoen2 cre file so skip short sword
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\)000,SET)\)~ 
      ~\1 CreateItem("leat04",0,0,0) XEquipItem("leat04",Myself,SLOT_ARMOR,EQUIP)~ // add studded leather
  END
  BUT_ONLY 
  
COPY_EXISTING ~jaheira.bcs~ ~override~ // jaheira gets sling & bullets at 16k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\)000,SET)\)~ 
      ~\1 CreateItem("bull01",20,0,0) XEquipItem("bull01",Myself,SLOT_AMMO0,EQUIP) CreateItem("slng01",0,0,0) XEquipItem("slng01",Myself,SLOT_WEAPON1,EQUIP)~ // add sling & bullets
  END
  BUT_ONLY 
  
COPY_EXISTING ~kagain.bcs~ ~override~ // kagain gets throwing axes at 2k, armor upgrades at 8 and 32
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\|4\|2\)000,SET)\)~ 
      ~\1 CreateItem("ax1h04",10,0,0) XEquipItem("ax1h04",Myself,SLOT_WEAPON1,EQUIP)~ // add throwing axes
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(16\|8\)000,SET)\)~ 
      ~\1 TransformItem("leat04","chan01")~ // studded leather > chain
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,32000,SET)\)~
      ~\1 TransformItem("leat04","chan04")~ // studded leather > splint
  END
  BUT_ONLY 
  
COPY_EXISTING ~khalid.bcs~ ~override~ // khalid gets bow & arrows at 2k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\|4\|2\)000,SET)\)~ 
      ~\1 CreateItem("arow01",20,0,0) XEquipItem("arow01",Myself,SLOT_AMMO0,EQUIP) CreateItem("bow03",0,0,0) XEquipItem("bow03",Myself,SLOT_WEAPON1,EQUIP)~ // add bow, arrows
  END
  BUT_ONLY 
  
COPY_EXISTING ~minsc.bcs~ ~override~ // minsc gets studded leather at 2k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\|4\|2\)000,SET)\)~ 
      ~\1 TransformItem("leat01","leat04")~ // leather > studded leather
  END
  BUT_ONLY 
  
COPY_EXISTING ~montaron.bcs~ ~override~ // montaron gets throwing axes at 16k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\)000,SET)\)~ 
      ~\1 CreateItem("ax1h04",10,0,0) XEquipItem("ax1h04",Myself,SLOT_WEAPON1,EQUIP)~ // add throwing axes
  END
  BUT_ONLY 
  
COPY_EXISTING ~safana.bcs~ ~override~ // montaron gets darts at 4k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\|4\)000,SET)\)~ 
      ~\1 CreateItem("dart01",20,0,0) XEquipItem("dart01",Myself,SLOT_WEAPON1,EQUIP)~ // add darts
  END
  BUT_ONLY 
  
COPY_EXISTING ~sharteel.bcs~ ~override~ // shar-teel gets xbow & bolts at 8k, armor upgrades at 8 and 32k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\|4\|2\)000,SET)\)~ 
      ~\1 CreateItem("bolt01",20,0,0) XEquipItem("bolt01",Myself,SLOT_AMMO0,EQUIP) CreateItem("xbow04",0,0,0) XEquipItem("xbow04",Myself,SLOT_WEAPON1,EQUIP)~ // add sling & bullets
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(16\|8\)000,SET)\)~ 
      ~\1 TransformItem("leat04","chan04")~ // studded leather > splint
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,32000,SET)\)~
      ~\1 TransformItem("leat04","plat01")~ // studded leather > plate
  END
  BUT_ONLY 
  
COPY_EXISTING ~skie.bcs~ ~override~ // skie gets short sword at 16k
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\|8\|4\|2\)000,SET)\)~ 
      ~\1 CreateItem("sw1h07",0,0,0) XEquipItem("sw1h07",Myself,SLOT_WEAPON1,EQUIP)~ // add short sword
  END
  BUT_ONLY 
  
COPY_EXISTING ~viconia.bcs~ ~override~ // viconia gets armor upgrades at 8k, 16k 
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,8000,SET)\)~ 
      ~\1 TransformItem("leat01","leat04")~ // leather > studded leather 
    REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,\(32\|16\)000,SET)\)~ 
      ~\1 TransformItem("leat01","chan01")~ // leather > chain
  END
  BUT_ONLY 

// tbd, cam
// tanar'ri do not hail from the nine hells; change albert to something more appropriate  
COPY_EXISTING ~albert.bcs~   ~override~
              ~rufcut01.bcs~ ~override~
              ~rufcut02.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Polymorph(TANARRI)~ ~Polymorph(CORNUGON)~  
  END
  BUT_ONLY  



// tbd, cam
// bad string refs for AI scripting
COPY_EXISTING ~bdimoenc.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~DisplayString(Myself,46150)~ ~DisplayString(Myself,31249)~ // *quaffs a potion*
  END
  BUT_ONLY
  
COPY_EXISTING ~bdeldotc.bcs~ ~override~
              ~bdgarrcc.bcs~ ~override~
              ~bdgarric.bcs~ ~override~
              ~bdkivanc.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~DisplayString(Myself,56560)~ ~DisplayString(Myself,31249)~ // *quaffs a potion*
  END
  BUT_ONLY
 
OUTER_SET use_wand = RESOLVE_STR_REF (@100)
COPY_EXISTING ~bddefai.bcs~  ~override~
              ~bddynac.bcs~  ~override~
              ~bdedwinc.bcs~ ~override~
              ~bdxzarc.bcs~  ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~DisplayString(Myself,66959)~ ~DisplayString(Myself,%use_wand%)~ // *uses a wand*
  END
  BUT_ONLY

/////                                                  \\\\\
///// area fixes                                       \\\\\
/////                                                  \\\\\

// bgcs-3454, cam
// lady's house lacks its normal music
COPY_EXISTING ~ar0132.are~ ~override~
  READ_LONG 0xbc song_off
  WRITE_LONG song_off 11
  BUT_ONLY

/////                                                  \\\\\
///// creature file fixes                              \\\\\
/////                                                  \\\\\

// tbd, cam
// crusaders with the wrong DV
COPY_EXISTING ~bdcrus21.cre~ ~override~
              ~bdcrus22.cre~ ~override~
              ~bdcrus6d.cre~ ~override~
              ~bdcrus7d.cre~ ~override~
              ~bdcrus8d.cre~ ~override~
              ~bdcrus9d.cre~ ~override~
  WRITE_ASCIIE 0x280 ~%SOURCE_RES%~ #32 // was bdcrus20
  BUT_ONLY IF_EXISTS // sod


// tbd, cam (originally from jmerry)
// joinable NPCs should have proper trueclass kit values
COPY_EXISTING ~ajanti.cre~   ~override~ // ajantis
              ~ajanti4.cre~  ~override~ // ajantis
              ~ajanti6.cre~  ~override~ // ajantis
              ~alora.cre~    ~override~ // alora
              ~alora6.cre~   ~override~ // alora
              ~coran.cre~    ~override~ // coran
              ~coran5.cre~   ~override~ // coran
              ~imoen1.cre~   ~override~ // imoen
              ~imoen2.cre~   ~override~ // imoen
              ~imoen4.cre~   ~override~ // imoen
              ~imoen6.cre~   ~override~ // imoen
              ~kivan.cre~    ~override~ // kivan
              ~kivan4.cre~   ~override~ // kivan
              ~kivan6.cre~   ~override~ // kivan
              ~sharte.cre~   ~override~ // shar-teel
  WRITE_LONG 0x244 0x40000000 // proper trueclass value
  BUT_ONLY IF_EXISTS

// tbd, cam
// HD too low for most ankhegs; bgee ones also had pickpocketable shells
INCLUDE ~eefixpack/files/tph/tbd_ankheg.tph~

//tbd, cam
// poison mists can be summoned but can also spawn as normal creatures, so need both a gender: neither and gender: summoned version
INCLUDE ~eefixpack/files/tph/tbd_poison_mists.tph~

// tbd, cam
// ordulian should give his cloak, or not have it. We choose that he doesn't have it
COPY_EXISTING ~orduli.cre~ ~override~
  LPF DELETE_CRE_ITEM STR_VAR item_to_delete = clck06 END
  BUT_ONLY

// BGCS-3442, cam
// bugbear shaman should use bugbear shaman animation
COPY_EXISTING ~bdbugb20.cre~ ~override~ // bugbear shaman
  WRITE_LONG 0x28 0x0000e291 // bugbear shaman animation  
  BUT_ONLY IF_EXISTS
  
// BGCS-483, luke
// CRE files should not use non-existing resources
WITH_SCOPE BEGIN
  COPY_EXISTING ~bdmcarri.cre~ ~override~ // Mutated Crawler – currently equipped with the non-existing "bdmcarri.itm"
    REPLACE_CRE_ITEM ~carrio1~ #0 #0 #0 ~unstealable&undroppable~ ~weapon~ EQUIP // the other mutated crawler file "bdcrawmu.cre" uses "carrio1", so we'll put "carrio1" here as well...
  BUT_ONLY IF_EXISTS // sod
END

WITH_SCOPE BEGIN
  // ~mepmag01.cre~ (Magma Mephit) – currently equipped with the non-existing "mepmag.itm"
  // Since there's no other Magma Mephit file, let us make "mepmag.itm" from scratch (based on BG2:EE)
  COPY_EXISTING "b1-4.itm" "override/mepmag.itm"
    // Header
    WRITE_LONG NAME1 "-1"
    WRITE_LONG NAME2 "-1"
    WRITE_LONG UNIDENTIFIED_DESC "-1"
    WRITE_LONG DESC "-1"
    // Extended header
    LPF "ALTER_ITEM_HEADER" INT_VAR "dicenumber" = 1 "dicesize" = 2 "damage_type" = 3 END // 1d2 (slashing)
    // Feature block
    LPF "ADD_ITEM_EFFECT" INT_VAR "type" = 1 "opcode" = 12 "target" = 2 "parameter2" = IDS_OF_SYMBOL ("DMGTYPE" "FIRE") "timing" = 1 "dicenumber" = 1 "dicesize" = 8 END // 1d8 (fire)
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Make sure plants are properly flagged as such
WITH_SCOPE BEGIN
  COPY_EXISTING "bddyingm.cre" "override" // Dying Myconid
    WRITE_BYTE 0x271 IDS_OF_SYMBOL ("GENERAL" "PLANT")
    WRITE_BYTE 0x272 IDS_OF_SYMBOL ("RACE" "MYCONID")
  BUT_ONLY IF_EXISTS // sod
END

/////                                                  \\\\\
///// item file fixes                                  \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_bgee_146.tph~ // tbd, cam: fixes items that cast spells

// tbd, cam
// special wand of magic missiles shouldn't bypass shield
COPY_EXISTING ~wand12.itm~ ~override~ // wand of missiles
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 324 target = 2 parameter1 = 126 parameter2 = 110 timing = 0 resist_dispel = 2 insert_point = 0 STR_VAR resource = wand12 END // block with shield spell
  LPF ADD_ITEM_EFFECT INT_VAR opcode =  61 target = 2 parameter1 = ((0 << 24) + (30 << 16) + (120 << 8)) parameter2 = (25 << 16) timing = 1 resist_dispel = 1 END // rgb fade
  BUT_ONLY
  


// luke
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/remove_redundant_portrait_icons.tph"
  LAUNCH_ACTION_FUNCTION "REMOVE_REDUNDANT_PORTRAIT_ICONS" STR_VAR "file_ext" = "itm" END
END

// luke
// Mis-indexed effects
WITH_SCOPE BEGIN
  COPY_EXISTING ~ption41.itm~ ~override~ // Potion of Power
    LAUNCH_PATCH_FUNCTION ~FJ_SPL_ITM_REINDEX~ END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Longtooth: The Grave Binder**
// - Unusually large dagger, depicted as a Short Sword => make sure `range=1`
WITH_SCOPE BEGIN
  COPY_EXISTING "dagg04.itm" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_ITEM_HEADER~ INT_VAR ~range~ = 1 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Paralytic Bolt +1**
// - `Save vs. Paralysis` negates (was `Save vs. Spell`)
// - `Weight` should be `0` (was `1`)
// - Hit target is stunned for `5 rounds` (was `6 rounds`)
// - `resist_dispel` should be `0` (effects should not be dispellable and should ignore MR)
WITH_SCOPE BEGIN
  COPY_EXISTING "sahbolt.itm" "override"
    /* Header */
    WRITE_LONG 0x4C 0
    /* Feature blocks */
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~check_globals~ = 0 ~match_opcode~ = 45 ~duration~ = 30 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~check_globals~ = 0 ~savingthrow~ = BIT2 ~resist_dispel~ = 0 END
  BUT_ONLY IF_EXISTS // sod
END

// luke
// – Neera's Staff +1 => When the staff strikes a target, there is a 10% chance that either the target or the wielder will take 1 point of fire damage
//// – In particular, on 10% of all hits there's an extra point of fire damage, so Neera gets hit 5% of the time and her target 5%
//// – This isn't implemented properly since the two op12 effects share the same probability range... Also, while we're at it, make sure it's 10% chance and not 11% chance...
WITH_SCOPE BEGIN
  COPY_EXISTING ~stafn1.itm~ ~override~ // Neera's Staff +1
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~check_globals~ = 0 ~multi_match~ = 1 ~match_opcode~ = 12 ~probability1~ = 4 END // 5% chance (target)
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~check_globals~ = 0 ~match_opcode~ = 12 ~match_probability1~ = 10 ~probability2~ = 5 ~probability1~ = 9 END // 10% chance (wielder)
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Chill Touch – better feedback for the player when attacking Golems or Undead
WITH_SCOPE BEGIN
  COPY_EXISTING "chillt.itm" "override"
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 177 END
    LAUNCH_PATCH_FUNCTION ~ADD_ITEM_EFFECT~ INT_VAR ~insert_point~ = 0 ~type~ = 1 ~target~ = 2 ~opcode~ = 324 ~parameter2~ = 55 STR_VAR ~resource~ = ~%DEST_RES%~ END // Immunity to resource and message
  BUT_ONLY_IF_IT_CHANGES
END

/*
luke
**Shocking Grasp**
- Should not interact with level-based spell protections
- Delete expiry sound since the spell can end prematurely
- It now casts a `SPL` file that scales with level (up to `20`)
  - (instead of using `20` different `ITM` files)
*/
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/shocking_grasp.tph"
  LAUNCH_ACTION_FUNCTION "WIZARD_SHOCKING_GRASP" END
END

// luke
// Ghoul Touch – should not interact with spell protections
WITH_SCOPE BEGIN
  COPY_EXISTING "ghoult.itm" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~power~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Melf's Minute Meteors – should not interact with spell protections
WITH_SCOPE BEGIN
  COPY_EXISTING "melfmet.itm" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~power~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

/*
luke
**Black Blade of Disaster**
- Should not interact with level-based spell protections
- Make sure *"The caster is considered to be proficient to the point of Grand Mastery in this weapon"* always works
  - `op233` needs to be on the normal effects list to be evaluated after chargen proficiency effects and override them!
- Provide a workaround for the following bugs:
  - if you Dual-class when the Black Blade is equipped, the character permanently gains grand mastery in long swords
  - if, for example, you have a warrior with an item that grants dagger proficiency while equipped, they can then take dagger specialization at a level-up, remove the item, and keep the specialization permanently
*/
WITH_SCOPE BEGIN
  WITH_TRA
    "eefixpack/languages/en_us/luke/shared.tra"
    "eefixpack/languages/%LANGUAGE%/luke/shared.tra"
  BEGIN
    INCLUDE "eefixpack/files/tph/luke/black_blade_of_disaster.tph"
    LAUNCH_ACTION_FUNCTION "WIZARD_BLACK_BLADE_OF_DISASTER" END
  END
END

// luke
// Energy Blade – should not interact with spell protections
WITH_SCOPE BEGIN
  COPY_EXISTING "eneblade.itm" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~power~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Flame Blade**
// - should not interact with level-based spell protections
WITH_SCOPE BEGIN
  COPY_EXISTING "fblade.itm" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~power~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Fire Seed**
// - should not interact with level-based spell protections
WITH_SCOPE BEGIN
  COPY_EXISTING "fireseed.itm" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~power~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Searing Orb**
// - should not interact with level-based spell protections
// - recode from scratch so as to use `op326` effects instead of messy `op177` effects
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/searing_orb.tph"
  LAUNCH_ACTION_FUNCTION ~CLERIC_SOL_SEARING_ORB~ END
END

/*
luke
**Magical Weapon Slot**
(Based on the previous tweak to the Black Blade of Disaster)
- Make sure natural creature weapons / touch attacks do not benefit from Fighting Styles
*/
WITH_SCOPE BEGIN
  WITH_TRA
    "eefixpack/languages/en_us/luke/shared.tra"
    "eefixpack/languages/%LANGUAGE%/luke/shared.tra"
  BEGIN
    INCLUDE "eefixpack/files/tph/luke/magical_weapon_slot.tph"
    LAUNCH_ACTION_FUNCTION "MAGICAL_WEAPON_SLOT" END
  END
END

/////                                                  \\\\\
///// spell fixes                                      \\\\\
/////                                                  \\\\\

// tbd, cam (from jmerry)
// simulacrum/projected images should have thief skills, but no shadow twin
INCLUDE ~eefixpack/files/tph/tbd_simulacrum.tph~

// luke
// **Cure Wounds**
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/cure_wounds.tph"
  LAUNCH_ACTION_FUNCTION ~CURE_WOUNDS~ END
END

// luke
// **Cause Wounds**
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/cause_wounds.tph"
  LAUNCH_ACTION_FUNCTION ~CAUSE_WOUNDS~ END
END

// luke
// **Lesser / Greater Restoration**
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/restoration.tph"
  LAUNCH_ACTION_FUNCTION ~RESTORATION~ END
END

// tbd, cam
// enrage is using berserk's spell icon
COPY_EXISTING ~spcl321.spl~ ~override~
  WRITE_ASCII 0x3a ~enrageb~ #8
  LPF ALTER_SPELL_HEADER STR_VAR icon = ~enrageb~ END
  BUT_ONLY

// tbd, cam
// shield spell shouldn't stack
COPY_EXISTING ~spwi114.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR match_opcode = 142 opcode = 321 power = 0 parameter2 = 0 timing = 1 duration = 0 STR_VAR insert = first resource = EVAL ~%SOURCE_RES%~ END
  BUT_ONLY

// tbd, cam
// know alignment's save should match description
COPY_EXISTING ~spwi208.spl~ ~override~ // know alignment, arcane - lacks saving throw
              ~sppr209.spl~ ~override~ // know alignment, divine - has -2 penalty
  LPF ALTER_EFFECT INT_VAR savingthrow = BIT0 savebonus = 0 END
  BUT_ONLY

// luke
// Mis-indexed effects
WITH_SCOPE BEGIN
  COPY_EXISTING ~%ERINYES_CHARM%.spl~ ~override~ // Charm Person
    LAUNCH_PATCH_FUNCTION ~FJ_SPL_ITM_REINDEX~ END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/remove_redundant_portrait_icons.tph"
  LAUNCH_ACTION_FUNCTION "REMOVE_REDUNDANT_PORTRAIT_ICONS" STR_VAR "file_ext" = "spl" END
END

// luke
// Mimic Glue should not set the GREASE stat
WITH_SCOPE BEGIN
  COPY_EXISTING ~%MIMIC_GLUE%.spl~ ~override~ // Mimic Glue
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "match_opcode" = 158 "opcode" = 215 "parameter2" = 1 STR_VAR "resource" = "GREASED" END // Grease overlay => Play visual effect, Mode: Over target (attached)
  BUT_ONLY_IF_IT_CHANGES
END



// luke
// Night Club +1 => THAC0: +2 at night (implemented via op232)
// 1) Remember that "blun38.spl" will be cast immediately whenever you load, and twice as often with haste, so you cannot avoid an overlapping duration without applying a leading op321 effect.
// 2) Similarly, remember that Slow will reduce the trigger of op232 to 200 ticks!
// As a result, we will double the duration of op54. I.e., We will not allow Slow and Haste to impact it (really, it does not make sense in this case)
// Additionally, sectype should be 0|NONE
WITH_SCOPE BEGIN
  COPY_EXISTING ~blun38.spl~ ~override~
    // Header
    WRITE_BYTE 0x27 0 // Secondary type: NONE
    // Feature blocks
    LPF "ADD_SPELL_EFFECT" INT_VAR "insert_point" = 0 "opcode" = 321 "target" = 1 "timing" = 1 STR_VAR "resource" = "%DEST_RES%" END // Remove effects by resource
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "match_opcode" = 54 "duration" = 13 END // Base THAC0 bonus => Some random duration value strictly greater than 200 ticks...
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Hamatula, Barbed Defense – Suppress misleading "One of the spell has failed" message
// 1) Set subspell's range to a very high value (max signed value)
// 2) Set subspell's projectile to either "RANGE05", "RANGE07" or "RANGE10". The last one is the most appropriate IMHO (given ABISHAI_BLUE selection circle)...
// NOTE: the combat log will still display string "Hamatula : Barbed Defense <TARGET>" when hit out of range!
//// as far as I know, there's no way to suppress it only when hit out of range...
WITH_SCOPE BEGIN
  COPY_EXISTING "bdbarbde.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_HEADER~ INT_VAR "range" = 0x7FFF "projectile" = IDS_OF_SYMBOL ("MISSILE" "OneTarget_Range_10") END
  BUT_ONLY IF_EXISTS // sod
END

// luke
// Lesser Fire Spirit – Suppress misleading "One of the spell has failed" message
// 1) Set subspell's range to a very high value (max signed value)
// 2) Set subspell's projectile to either "RANGE05", "RANGE07" or "RANGE10". The second one is the most appropriate IMHO (given ELEMENTAL_FIRE_SMALL selection circle)...
WITH_SCOPE BEGIN
  COPY_EXISTING "bdsha12a.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_HEADER~ INT_VAR "range" = 0x7FFF "projectile" = IDS_OF_SYMBOL ("MISSILE" "OneTarget_Range_07") END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Shield of Barnassus: The Suncatcher – Suppress misleading "One of the spell has failed" message
// 1) Set subspell's range to a very high value (max signed value)
// 2) Set subspell's projectile to either "RANGE05", "RANGE07" or "RANGE10". The second one is the most appropriate IMHO (given it's a player-usable item + all player animations have the same selection circle)...
WITH_SCOPE BEGIN
  COPY_EXISTING "bdshld02.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_SPELL_HEADER~ INT_VAR "range" = 0x7FFF "projectile" = IDS_OF_SYMBOL ("MISSILE" "OneTarget_Range_07") END
  BUT_ONLY IF_EXISTS // sod
END

// luke
// Grease – Remove duplicate op321 effect
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_GREASE%.spl~ ~override~ // Grease
    PATCH_WITH_SCOPE BEGIN
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "321" "resource" = "%DEST_RES%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 321 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ STR_VAR ~match_resource~ = ~%DEST_RES%~ END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Magic Missile – better feedback for the player when you cast it at someone protected by WIZARD_SHIELD
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_MAGIC_MISSILE%.spl~ ~override~
                ~%TRAP_MAGIC_MISSILE%.spl~ ~override~
                ~%MAGIC_MISSILE_SURE_HIT%.spl~ ~override~
                ~%BEHOLDER_MAGIC_MISSILE%.spl~ ~override~
    LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ INT_VAR "insert_point" = 0 "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("SPLSTATE" "WIZARD_SHIELD") "parameter2" = 110 "resist_dispel" = 2 "power" = 1 STR_VAR "resource" = "%DEST_RES%" END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Larloch's Minor Drain
// 1) subspell's effects should have `power=0`
// 2) subspell should not play `casting graphics` and `casting sound`
// 3) subspell should not use the `exclusion flags` field
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_LARLOCH_MINOR_DRAIN%a.spl~ ~override~
                ~%INNATE_LARLOCHS_MINOR_DRAIN%a.spl~ ~override~
    // Header
    WRITE_ASCII 0x10 "" #8
    WRITE_LONG 0x1E 0
    WRITE_SHORT 0x22 0
    // Feature blocks
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "power" = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Chill Touch – should not interact with spell protections
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_CHILL_TOUCH%a.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~power~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Detect Evil – should not interact with spell protections
// 1) there's no need for the subspell
//// 1a) its effects have incorrect power levels
//// more importantly, as of v2.6, you should no longer be able to bypass other's Magic Resistance by targeting yourself with an area-effect ability => see `https://gibberlings3.github.io/iesdp/file_formats/ie_formats/eff_v2.htm#effv2_Body_0x5C` (BIT3) for further details...
// 2) creatures flagged as STATE_NONDETECTION (see f.i. "Cloak of Non-Detection: Whispers of Silence") should not be immune to it
//// I mean, STATE_NONDETECTION protects against op47 and op116; this spell uses op115, so... Am I missing something?
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_DETECT_EVIL%.spl" "override"
                "%CLERIC_DETECT_EVIL%.spl" "override"
                "%PALADIN_DETECT_EVIL%.spl" "override"
                "spin120.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 146 ~opcode~ = 115 ~parameter1~ = 0 ~parameter2~ = 0 ~timing~ = 1 ~duration~ = 0 STR_VAR ~resource~ = ~~ END // Cast spell => Detect alignment
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 318 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Stinking Cloud
// 1) op39 can automatically display a portrait icon, there's no need to apply a separate op142 effect
// 2) remove the "Bypass Mirror Image" flag (it's only relevant for opcode #12 and opcode #25)
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_STINKING_CLOUD%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "_" => "ab_off" BEGIN
        GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
        PHP_EACH "fx_array" AS "_" => "fx_off" BEGIN
          WRITE_LONG ("%fx_off%" + 0x24) (THIS BAND BNOT BIT24)
        END
      END
    END
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 142 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 39 ~special~ = 126 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Stinking Cloud
// 1) op39 can automatically display a portrait icon, there's no need to apply a separate op142 effect
// 2) remove the "Bypass Mirror Image" flag (it's only relevant for opcode #12 and opcode #25)
// 3) provide better feedback for the player when attacking Golems or Undead
WITH_SCOPE BEGIN
  COPY_EXISTING "%TRAP_STINKING_CLOUD%.spl" "override"
                "%MEPHIT_STINKING_CLOUD%.spl" "override"
                "spwm187.spl" "override" // Wild surge
    PATCH_WITH_SCOPE BEGIN
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "_" => "ab_off" BEGIN
        GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
        PHP_EACH "fx_array" AS "_" => "fx_off" BEGIN
          WRITE_LONG ("%fx_off%" + 0x24) (THIS BAND BNOT BIT24)
        END
      END
    END
    LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ INT_VAR ~insert_point~ = 0 ~target~ = 2 ~opcode~ = 324 ~parameter2~ = 55 STR_VAR ~resource~ = ~%DEST_RES%~ END // Immunity to resource and message
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 142 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 39 ~special~ = 126 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Chaos Shield – remove duplicate op328 effect
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_CHAOS_SHIELD%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "CHAOS_SHIELD")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "CHAOS_SHIELD") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Deafness
// 1) should not bypass Magic Resistance
// 2) should be dispellable
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_DEAFNESS%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~resist_dispel~ = BIT0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Flame Arrow – op12 effects should have a duration of 0 (this is probably irrelevant, but just in case...)
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_FLAME_ARROW%.spl~ ~override~
                ~%FORCE_DAMAGE_1%.spl~ ~override~
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "match_opcode" = 12 "match_timing" = 1 "duration" = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Lightning Bolt – remove duplicate op318 effect
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_LIGHTNING_BOLT%.spl" "override"
                "%TALOS_LIGHTNING_BOLT%.spl" "override"
                "%TRAP_LIGHTNING_BOLT%.spl" "override"
                "spdr301.spl" "override" // Avenger
                "%CELESTIAL_BOLT%.spl" "override"
                "%GORION_LIGHTNING%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "318" "resource" = "%DEST_RES%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 318 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ STR_VAR ~match_resource~ = ~%DEST_RES%~ END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Protection From Normal Missiles – remove duplicate op328 effect
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_PROTECTION_FROM_NORMAL_MISSILES%.spl" "override"
                "spra303.spl" "override" // Stalker
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "PROTECTION_FROM_NORMAL_MISSILES")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "PROTECTION_FROM_NORMAL_MISSILES") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Vampiric Touch
// 1) subspell's effects should have `power=0`
// 2) subspell should not play `casting graphics` and `casting sound`
// 3) subspell's type should match its parent (to maintain casting level!)
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_VAMPIRIC_TOUCH%a.spl~ ~override~
    // Header
    WRITE_ASCII 0x10 "" #8
    WRITE_SHORT 0x1C 1 // wizard
    WRITE_SHORT 0x22 0
    // Feature blocks
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "power" = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Vampiric Touch (innate)
// 1) subspell's effects should have `power=0`
// 2) subspell should not play `casting graphics` and `casting sound`
WITH_SCOPE BEGIN
  COPY_EXISTING ~%INNATE_VAMPIRIC_TOUCH%a.spl~ ~override~
                ~%TANARI_VAMPIRIC_TOUCH%a.spl~ ~override~
    // Header
    WRITE_ASCII 0x10 "" #8
    WRITE_SHORT 0x22 0
    // Feature blocks
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "power" = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Wraithform – make sure all limited effects are dispellable
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_WRAITH_FORM%.spl~ ~override~
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "resist_dispel" = (BIT0 + BIT1) END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Minor Spell Deflection – remove duplicate op328 / op233 effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_MINOR_SPELL_DEFLECTION%.spl" "override"
                "spra302.spl" "override" // Stalker
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS")
      SET "op233_p2" = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_DEFLECTION")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" = "%op233_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_DEFLECTION") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Improved Invisibility
// Applying "Improved" Invisibility will cause the effect to be duplicated, with the duplicate using "Normal" invisibility.
//// There's no reason to apply both Normal and Improved invisibility in the same spell/item (unless you want them to have different probabilities/hd limits, but this is not the case).
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_IMPROVED_INVISIBILITY%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 20 ~match_parameter2~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Emotion, Hopelessness
// Op39 can automatically display a portrait icon, there's no need to apply a separate op142 effect
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_EMOTION_HOPELESSNESS%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 142 ~match_parameter2~ = 44 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 39 ~special~ = 44 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Cloudkill
// Remove the "Bypass Mirror Image" flag from op55 / op215 / op174 (it's only relevant for opcode #12)
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_CLOUDKILL%.spl" "override"
                "%TRAP_CLOUDKILL%.spl" "override"
                "%SPORE_DEATH%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "_" => "ab_off" BEGIN
        GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
        PHP_EACH "fx_array" AS "_" => "fx_off" BEGIN
          PATCH_MATCH SHORT_AT ("%fx_off%" + 0x0) WITH
            "12" BEGIN END
            DEFAULT
              WRITE_LONG ("%fx_off%" + 0x24) (THIS BAND BNOT BIT24)
          END
        END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Shadow Door
// Applying "Improved" Invisibility will cause the effect to be duplicated, with the duplicate using "Normal" invisibility.
//// There's no reason to apply both Normal and Improved invisibility in the same spell/item (unless you want them to have different probabilities/hd limits, but this is not the case).
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_SHADOW_DOOR%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 20 ~match_parameter2~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Feeblemind – make sure it is truly permanent (until dispelled or character death)
//// Unlike other effects that alter a creature's stat, this one does leave behind a removable effect when applied with `timing=1`
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_FEEBLEMIND%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 76 ~timing~ = 1 ~duration~ = 0 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 142 ~timing~ = 1 ~duration~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Protection From Normal Weapons – remove duplicate op328 effect
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_PROTECTION_FROM_NORMAL_WEAPONS%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "PROTECTION_FROM_NORMAL_WEAPONS")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "PROTECTION_FROM_NORMAL_WEAPONS") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Minor Spell Turning – remove duplicate op328 / op233 effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_MINOR_SPELL_TURNING%.spl" "override"
                "%BEHOLDER_SPELL_TURNING%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS")
      SET "op233_p2" = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_TURNING")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" = "%op233_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_TURNING") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Improved Haste
// 1) make sure all effects bypass Magic Resistance
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_IMPROVED_HASTE%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~resist_dispel~ = (BIT0 + BIT1) END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Spell Deflection – remove duplicate op328 / op233 effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_SPELL_DEFLECTION%.spl" "override"
                "%SPELL_DEFLECTION_NO_VISUAL%.spl" "override"
                "spwi802.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS")
      SET "op233_p2" = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_DEFLECTION")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" = "%op233_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_DEFLECTION") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Stone to Flesh – make sure all effects bypass Magic Resistance
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_STONE_TO_FLESH%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~resist_dispel~ = BIT1 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Spell Turning – remove duplicate op328 / op233 effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_SPELL_TURNING%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS")
      SET "op233_p2" = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_TURNING")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" = "%op233_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "WIZARD_SPELL_TURNING") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Sphere of Chaos – make sure all effects are subjected to Magic Resistance
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_SPHERE_OF_CHAOS%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~resist_dispel~ = BIT0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Mass Invisibility
// 1) Applying "Improved" Invisibility will cause the effect to be duplicated, with the duplicate using "Normal" invisibility.
//// There's no reason to apply both Normal and Improved invisibility in the same spell/item (unless you want them to have different probabilities/hd limits, but this is not the case).
// 2) "Improved" Invisibility naturally provides a bonus of 4 to all saves, there's no need for separate op33-37 effects.
WITH_SCOPE BEGIN
  COPY_EXISTING ~%WIZARD_MASS_INVISIBILITY%.spl~ ~override~
    PATCH_WITH_SCOPE BEGIN
      PATCH_FOR_EACH "match_opcode" IN "33" "34" "35" "36" "37" BEGIN
        LAUNCH_PATCH_FUNCTION "DELETE_EFFECT" INT_VAR "match_opcode" END
      END
    END
    LAUNCH_PATCH_FUNCTION "DELETE_EFFECT" INT_VAR "match_opcode" = 20 "match_parameter2" = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Improved Chaos Shield – remove duplicate op328 effect
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_IMPROVED_CHAOS_SHIELD%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "IMPROVED_CHAOS_SHIELD")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "IMPROVED_CHAOS_SHIELD") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Spell Trap – remove duplicate op328 / op282 effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_SPELL_TRAP%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "282" "parameter2" = "8" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 282 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = 8 END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// Energy Drain – provide better feedback for the player
WITH_SCOPE BEGIN
  COPY_EXISTING "%WIZARD_ENERGY_DRAIN%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~CLONE_EFFECT~ INT_VAR ~match_opcode~ = 216 ~opcode~ = 139 ~parameter1~ = 25803 STR_VAR ~insert~ = ~below~ END // Display string
  BUT_ONLY_IF_IT_CHANGES
END

// tbd, cam
// don't play berserk damage warnings on non-party memebers; see also spin117[ab].eff
INCLUDE ~eefixpack/files/tph/tbd_angry_noises.tph~

// luke
// **Command**
// - Creatures with `37` or more Hit Dice (or experience levels) should not be immune to it
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_COMMAND%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_dicenumber~ = 36 ~dicenumber~ = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

/*// luke
// **Sanctuary**
// - Ending sound effect should play after `60` seconds (not immediately)
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_SANCTUARY%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 174 ~duration~ = 60 END
  BUT_ONLY_IF_IT_CHANGES
END*/

// luke
// **Armor of Faith**
// - remove duplicate `op328` effect(s)
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_ARMOR_OF_FAITH%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      SET "parameter2" = IDS_OF_SYMBOL ("SPLSTATE" "ARMOR_OF_FAITH")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "ARMOR_OF_FAITH") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Barkskin**
// - Instantaneous effects should have a duration of `0` (this is probably irrelevant, but just in case...)
WITH_SCOPE BEGIN
  COPY_EXISTING ~%CLERIC_BARKSKIN%.spl~ ~override~
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "match_timing" = 1 "duration" = 0 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Slow Poison**
// - Make sure it truly slows poison (so as to match `IWDEE` and spell description/name)
WITH_SCOPE BEGIN
  COPY_EXISTING ~%CLERIC_SLOW_POISON%.spl~ ~override~
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "match_opcode" = 11 "opcode" = 329 "parameter1" = 10 "parameter2" = 0 END // Cure poison => Slow poison
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Summon Insects, Insect Plague**
// - remove duplicate `op233` effect(s)
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_SUMMON_INSECTS%.spl" "override"
                "%CLERIC_INSECT_PLAGUE%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      SET "parameter2" = IDS_OF_SYMBOL ("STATS" "CLERIC_INSECT_PLAGUE")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_globals~ = 0 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "CLERIC_INSECT_PLAGUE") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Poison**
// - Make sure `duration=60 // 1 turn` (instead of `62`)
// - Use multiple `op25` effects instead of `spwi411[a-e].eff` (so as to make it work with `CLERIC_SLOW_POISON`)
//// - As far as I know, `op329` cannot slow poison if `op25` is applied via `op177`...
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_POISON%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 177 END // Use EFF file
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        FOR ("i" = 1 ; "%i%" <= "%hdr%" + 2 ; "i" += 1) BEGIN
          LAUNCH_PATCH_FUNCTION ~CLONE_EFFECT~ INT_VAR ~header~ = ~%hdr%~ ~match_opcode~ = 12 ~opcode~ = 25 ~parameter1~ = 6 ~parameter2~ = 3 ~timing~ = 0 ~duration~ = 60 ~dicenumber~ = 0 ~dicesize~ = 0 ~special~ = 0 STR_VAR ~insert~ = ~below~ ~resource~ = ~~ END
        END
      END
    END
    LAUNCH_PATCH_FUNCTION "DELETE_EFFECT" INT_VAR "check_globals" = 0 "match_opcode" = 142 "match_parameter2" = 6 END // op25 naturally provides the "Poisoned" portrait icon
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_duration~ = 62 ~duration~ = 60 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Spirit Fire**
// - should not interact with level-based spell protections
//// - As a result, do not use "bddoom.spl" (since its effects are coded as `power=1`)
// - *"Spirits, fey creatures, elementals, and spectral undead take double damage."* => make sure this double damage bypasses Mirror Image (since the parent `SPL` file is an AoE spell)
WITH_SCOPE BEGIN
  INCLUDE "eefixpack/files/tph/luke/cleric_spirit_fire.tph"
  LAUNCH_ACTION_FUNCTION "CLERIC_SPIRIT_FIRE" END
END

// luke
// **Chaotic Commands**
// - remove duplicate `op328` effect(s)
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_CHAOTIC_COMMANDS%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      SET "parameter2" = IDS_OF_SYMBOL ("SPLSTATE" "CHAOTIC_COMMANDS")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_globals~ = 0 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "CHAOTIC_COMMANDS") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Blade Barrier**
// - Make sure `duration=60 // 1 turn` (instead of `62`)
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_BLADE_BARRIER%.spl" "override"
                "%NPC_BLADE_BARRIER%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_timing~ = 0 ~duration~ = 60 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_timing~ = 4 ~duration~ = 60 END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Dolorous Decay**
// - Make sure all poison-related effects have a duration of `50` (instead of `56`)
// - Make sure the "Decaying" portrait icon does not bypass Magic Resistance
// - Make sure the delayed `op174` effect (the one related with Poison) offers a Save vs. Poison @ -2
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_DOLOROUS_DECAY%.spl" "override"
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 142 "match_parameter2" = 101 ~resist_dispel~ = BIT0 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_duration~ = 56 ~duration~ = 50 END
    LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ INT_VAR ~match_opcode~ = 174 "match_timing" = 4 "match_duration" = 50 ~savingthrow~ = BIT2 ~savebonus~ = ~-2~ END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Physical Mirror**
// - remove duplicate `op328` / `op233` effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_PHYSICAL_MIRROR%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_WEAPONS")
      SET "op233_p2" = IDS_OF_SYMBOL ("STATS" "CLERIC_PHYSICAL_MIRROR")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_WEAPONS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" = "%op233_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "CLERIC_PHYSICAL_MIRROR") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Shield of the Archons**
// - remove duplicate `op328` / `op233` effects
WITH_SCOPE BEGIN
  COPY_EXISTING "%CLERIC_SHIELD_OF_THE_ARCHONS%.spl" "override"
    PATCH_WITH_SCOPE BEGIN
      SET "op328_p2" = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS")
      SET "op233_p2" = IDS_OF_SYMBOL ("STATS" "CLERIC_SHIELD_OF_THE_ARCHONS")
      GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
      PHP_EACH "ab_array" AS "hdr" => "ab_off" BEGIN
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "328" "parameter2" = "%op328_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 328 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("SPLSTATE" "BUFF_PRO_SPELLS") END
        LPF "COUNT_V10_HEAD_EFFECTS" STR_VAR "opcode" = "233" "parameter2" = "%op233_p2%" RET "count" END
        LPF ~DELETE_EFFECT~ INT_VAR ~match_opcode~ = 233 ~check_headers~ = (~%count%~ <= 1 ? 0 : 1) ~multi_match~ = (~%count%~ - 1) ~header~ = ~%hdr%~ ~match_parameter2~ = IDS_OF_SYMBOL ("STATS" "CLERIC_SHIELD_OF_THE_ARCHONS") END
      END
    END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Nature's Beauty**
// - permanent blindness (until dispelled)
//   1. Timing Mode 1 will permanently set the creature's STATE_BLIND flag, leaving no removable effect.
//   2. Related: the -4 penalty to AC and THAC0 are apparently tied to the effect (op74), not STATE_BLIND.
// - As a result, we suggest to stick with a limited `timing_mode` and set `duration` to a very high random value (max signed value...?).
// - Additionally, this spell should only affect HUMANOID (there are a lot of unnatural creatures that are immune to it via op206; also, it is intended, right...?)
WITH_SCOPE BEGIN
  COPY_EXISTING ~%CLERIC_NATURE_BEAUTY%.spl~ ~override~
    LPF "ADD_SPELL_EFFECT" INT_VAR "insert_point" = 0 "opcode" = 324 "target" = 2 "power" = 7 "parameter2" = 6 "resist_dispel" = BIT1 STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message (GENERAL != HUMANOID)
    LAUNCH_PATCH_FUNCTION "ALTER_EFFECT" INT_VAR "match_opcode" = 74 "timing" = 0 "duration" = 0x7FFFFFFF END // Blindness
    LAUNCH_PATCH_FUNCTION "DELETE_EFFECT" INT_VAR "check_globals" = 0 "match_opcode" = 142 "match_parameter2" = 8 END // op74 naturally provides the "Blind" portrait icon
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Sunray**
// - recode from scratch so as to use `op326` effects instead of messy `op177` effects
// - its insta-death effect against `UNDEAD` is now blocked by `"%CLERIC_DEATH_WARD%"` and the like
WITH_SCOPE BEGIN
  INCLUDE ~eefixpack/files/tph/luke/sunray.tph~
  LAF ~CLERIC_SUNRAY~ END
END

// luke
// **Holy / Unholy Word**
// - recode from scratch so as to use `op324` magic instead of messy `op177` effects
// - make sure it causes arcane spell failure (not just divine spell failure)
WITH_SCOPE BEGIN
  INCLUDE ~eefixpack/files/tph/luke/holy_unholy_word.tph~
  LAF ~CLERIC_HOLY_UNHOLY_WORD~ END
END

// luke
// **Creeping Doom**
// - remove duplicate `op233` effect(s)
// - *"For each round the victim remains inside the cloud, <PRO_HESHE> must make a Save vs. Spell at -2 or run away in fear for one round."* => match `BG2EE` implementation
WITH_SCOPE BEGIN
  INCLUDE ~eefixpack/files/tph/luke/creeping_doom.tph~
  LAF ~CLERIC_CREEPING_DOOM~ END
END

// luke
// **Creeping Doom** (Black Dragon)
// - use same projectile as `"%CLERIC_CREEPING_DOOM%"`
WITH_SCOPE BEGIN
  COPY_EXISTING "%BLACK_DRAGON_INSECT%.spl" "override"
    LPF "ALTER_SPELL_HEADER" INT_VAR "projectile" = IDS_OF_SYMBOL ("MISSILE" "Chain_Insect") END
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Absorb Health** (Blackguard)
// - should not bypass Magic Resistance
// - subspell should scale up to level `40` (`BG2` level cap)
// - subspell's Primary/Secondary type should match parent `SPL` file
// - subspell should not play a casting sound
WITH_SCOPE BEGIN
  WITH_SCOPE BEGIN
    COPY_EXISTING "%BLACKGUARD_ABSORB_HEALTH%.spl" "override"
      LPF "ALTER_EFFECT" INT_VAR "match_opcode" = 146 "resist_dispel" = BIT0 END
    BUT_ONLY_IF_IT_CHANGES
  END
  WITH_SCOPE BEGIN
    COPY_EXISTING "spdn01a.spl" "override"
      WRITE_ASCII 0x10 "" #8 // Casting sound
      WRITE_BYTE 0x25 7 // Primary type: NECROMANCER
      WRITE_BYTE 0x27 10 // Secondary type: OFFENSIVEDAMAGE
      /* Feature blocks */
      LPF "CD_EXTEND-O-MATIC" INT_VAR "level_cap" = 40 END
      PATCH_WITH_SCOPE BEGIN
        GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
        PHP_EACH "ab_array" AS "_" => "ab_off" BEGIN
          GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
          PHP_EACH "fx_array" AS "_" => "fx_off" BEGIN
            PATCH_MATCH (SHORT_AT "%fx_off%") WITH
              "12" BEGIN
                WRITE_LONG ("%fx_off%" + 0x4) (SHORT_AT ("%ab_off%" + 0x10) * 2) // 2 points of damage per level
              END
              DEFAULT
            END
          END
        END
      END
    BUT_ONLY_IF_IT_CHANGES
  END
END

/////                                                  \\\\\
///// projectile fixes                                 \\\\\
/////                                                  \\\\\

// luke
// Ice Storm should last 4 rounds (not 3)
WITH_SCOPE BEGIN
  COPY_EXISTING ~icestorm.pro~ ~override~
    WRITE_BYTE 0x216 4 // # repetitions
  BUT_ONLY_IF_IT_CHANGES
END

// luke
// **Insect Plague**
// - make sure it affects up to 6 creatures (as per spell description)
WITH_SCOPE BEGIN
  COPY_EXISTING ~insec2.pro~ ~override~
    WRITE_SHORT 0x200 (THIS BOR BIT15) // Single target
    WRITE_SHORT 0x244 6 // # dice for multiple targets
    WRITE_SHORT 0x246 1 // Dice size for multiple targets
  BUT_ONLY_IF_IT_CHANGES
END

/////                                                  \\\\\
///// store fixes                                      \\\\\
/////                                                  \\\\\

// tbd, cam (from jmerry)
// Correct number of charges on the Sandthief's Ring at tier 3
COPY_EXISTING ~bpxith03.sto~ ~override~ 
  LPF REPLACE_STORE_ITEM INT_VAR number_in_stock = 1 charges1 = 8 charges2 = 0 charges3 = 0 STR_VAR old_item = ring05 new_item = ring05 END
  BUT_ONLY 

/////                                                  \\\\\
///// misc/other fixes                                 \\\\\
/////                                                  \\\\\

/////                                                  \\\\\
///// final batch of INCLUDEs and cross-patching       \\\\\
/////                                                  \\\\\

INCLUDE ~eefixpack/files/tph/tbd_bgee_probabilities.tph~ // tbd, cam: probability fixes
INCLUDE "%MOD_FOLDER%/files/tph/dw_fixes.tph" // tbd, davidw:  318/324 immunity, 109/175/185 disentanglement, and more